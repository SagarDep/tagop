apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        applicationId "com.akarbowy.tagop"
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 5
        versionName "1.1.1"

        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-DEBUG"
            ext.enableCrashlytics = false

            manifestPlaceholders = [appName: "Tagop-debug"]
        }

        release {
            debuggable false
            shrinkResources false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            manifestPlaceholders = [appName: "Tagop"]
        }

        applicationVariants.all { variant ->
            variant.buildConfigField("String", "API_KEY", getSecretValueOf("API_KEY"))
        }
    }

    productFlavors {
        mock {
            applicationIdSuffix = ".mock"
        }
        prod {

        }
    }

    android.variantFilter { variant ->
        if (variant.buildType.name.equals('release')
                && variant.getFlavors().get(0).name.equals('mock')) {
            variant.setIgnore(true);
        }
    }
}

static def getSecretValueOf(String key) {
    def Properties p = new Properties()
    p.load(new FileInputStream(new File("secrets.properties")))
    return "\"" + p.getProperty(key) + "\""
}

ext {
    supportLibVersion = '25.1.1'
    retrofitVersion = '2.1.0'
    okHttpVersion = '3.3.1'
    stethoVersion = '1.3.1'
    daggerVersion = '2.6.1'
    timberVersion = '4.1.2'
    ottoVersion = '1.3.8'
    butterknfieVersion = '8.4.0'
    frescoVersion = '0.13.0'
    frescoImageViewerVersion = '0.5.0'
    crashlyticsVersion = '2.6.4@aar'
    partdefinerVersion = '1.0'

    junitVersion = '4.12'
    mockitoVersion = '1.10.19'
    hamcrestVersion = '1.3'
    robolectricVersion = '3.1.2'
    annotationsVersion = '25.1.1'
    runnerVersion = '0.5'
    espressoVersion = '2.2.2'
}

dependencies {
    // Dependencies for tests
    testCompile "junit:junit:$junitVersion"
    testCompile "org.mockito:mockito-all:$mockitoVersion"
    testCompile "org.hamcrest:hamcrest-all:$hamcrestVersion"
    testCompile "org.robolectric:robolectric:$robolectricVersion"
    androidTestCompile "com.android.support:support-annotations:$annotationsVersion"
    androidTestCompile "com.android.support.test:runner:$runnerVersion"
    androidTestCompile "com.android.support.test.espresso:espresso-core:$espressoVersion"

    compile "com.android.support:appcompat-v7:$supportLibVersion"
    compile "com.android.support:recyclerview-v7:$supportLibVersion"
    compile "com.android.support:design:$supportLibVersion"

    //network
    compile "com.squareup.retrofit2:retrofit:$retrofitVersion"
    compile "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    compile "com.squareup.okhttp3:logging-interceptor:$okHttpVersion"
    compile "com.facebook.stetho:stetho:$stethoVersion"
    compile "com.facebook.stetho:stetho-okhttp3:$stethoVersion"

    //dagger
    compile "com.google.dagger:dagger:$daggerVersion"
    annotationProcessor "com.google.dagger:dagger-compiler:$daggerVersion"
    provided "org.glassfish:javax.annotation:10.0-b28"

    //logging
    compile "com.jakewharton.timber:timber:$timberVersion"

    //event bus
    compile "com.squareup:otto:$ottoVersion"

    //binding
    compile "com.jakewharton:butterknife:$butterknfieVersion"
    annotationProcessor "com.jakewharton:butterknife-compiler:$butterknfieVersion"

    //image
    compile "com.facebook.fresco:fresco:$frescoVersion"
    compile "com.github.stfalcon:frescoimageviewer:$frescoImageViewerVersion"
    compile "com.facebook.fresco:animated-gif:$frescoVersion"

    //crashlytics
    compile("com.crashlytics.sdk.android:crashlytics:$crashlyticsVersion") { transitive = true; }

    compile "com.akarbowy:partdefiner:$partdefinerVersion"
}
