apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.2"

    defaultConfig {
        applicationId "com.akarbowy.tagop"
        minSdkVersion 16
        targetSdkVersion 24
        versionCode 2
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-DEBUG"
            ext.enableCrashlytics = false

            manifestPlaceholders = [appName: "Tagop-debug"]
        }

        release {
            debuggable false
            shrinkResources false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            manifestPlaceholders = [appName: "Tagop"]
        }

        applicationVariants.all { variant ->
            variant.buildConfigField("String", "API_KEY",  getSecretValueOf("API_KEY"))
//            variant.buildConfigField("String", "API_KEY", "\"uncomment when running database config\"")
        }
    }
}

def getSecretValueOf(String key) {
    def Properties p = new Properties()
    p.load(new FileInputStream(new File("secrets.properties")))
    return "\"" + p.getProperty(key) + "\""
}

ext {
    supportLibVersion = '24.2.1'
    retrofitVersion = '2.1.0'
    okHttpVersion = '3.3.1'
    stethoVersion = '1.3.1'
    daggerVersion = '2.6.1'
    timberVersion = '4.1.2'
    ottoVersion = '1.3.8'
    butterknfieVersion = '8.4.0'
    ormVersion = '5.0'
    frescoVersion = '0.13.0'
    frescoImageViewerVersion = '0.1.0'
    crashlyticsVersion = '2.6.4@aar'
    partdefinerVersion = '1.0'
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'junit:junit:4.12'
    testCompile "org.robolectric:robolectric:3.1.2"
    androidTestCompile 'com.android.support:support-annotations:24.2.1'
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.2'
    compile 'com.android.support.test.espresso:espresso-idling-resource:2.2.2'
    androidTestCompile('com.android.support.test.espresso:espresso-contrib:2.2') {
        // Necessary to avoid version conflicts
        exclude group: 'com.android.support', module: 'appcompat'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude module: 'recyclerview-v7'
    }

    compile "com.android.support:appcompat-v7:$supportLibVersion"
    compile "com.android.support:recyclerview-v7:$supportLibVersion"
    compile "com.android.support:design:$supportLibVersion"

    //network
    compile "com.squareup.retrofit2:retrofit:$retrofitVersion"
    compile "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    compile "com.squareup.okhttp3:logging-interceptor:$okHttpVersion"
    compile "com.facebook.stetho:stetho:$stethoVersion"
    compile "com.facebook.stetho:stetho-okhttp3:$stethoVersion"

    //dagger
    compile "com.google.dagger:dagger:$daggerVersion"
    annotationProcessor "com.google.dagger:dagger-compiler:$daggerVersion"
    provided "org.glassfish:javax.annotation:10.0-b28"

    //database
    compile "com.j256.ormlite:ormlite-core:$ormVersion"
    compile "com.j256.ormlite:ormlite-android:$ormVersion"

    //logging
    compile "com.jakewharton.timber:timber:$timberVersion"

    //event bus
    compile "com.squareup:otto:$ottoVersion"

    //binding
    compile "com.jakewharton:butterknife:$butterknfieVersion"
    annotationProcessor "com.jakewharton:butterknife-compiler:$butterknfieVersion"

    //image
    compile "com.facebook.fresco:fresco:$frescoVersion"
    compile "com.github.stfalcon:frescoimageviewer:$frescoImageViewerVersion"

    //crashlytics
    compile("com.crashlytics.sdk.android:crashlytics:$crashlyticsVersion") { transitive = true; }

    compile "com.akarbowy:partdefiner:$partdefinerVersion"
}
